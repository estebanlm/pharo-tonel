"
This class represents the symbols containing 32bit characters.
"
Class {
	#name : #WideSymbol,
	#superclass : #Symbol,
	#type : #words,
	#category : #Collections-Strings,
	#timestamp : '<historical>'
}

{
	#category : #converting,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>asWideString  [

	^ self.
]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>at: index  [
	"Answer the Character stored in the field of the receiver indexed by the argument."
	^ Character value: (self wordAt: index).

]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>byteAt: index [

	| d r |
	d := (index + 3) // 4.
	r := (index - 1) \\ 4 + 1.
	^ (self wordAt: d) digitAt: ((4 - r) + 1).

]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>byteAt: index put: aByte [
	self errorNoModification.
]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>byteSize [

	^ self size * 4.

]

{
	#category : #comparing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>findIn: body startingAt: start matchTable: matchTable [
	
	^ self findSubstring: self in: body startingAt: start matchTable: matchTable
]

{
	#category : #testing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>isWideString [
	"Answer whether the receiver is a WideString"
	^true
]

{
	#category : #private,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>privateAt: index put: aCharacter [
	"Primitive. Store the Character in the field of the receiver indicated by
	the index. Fail if the index is not an Integer or is out of bounds, or if
	the argument is not a Character. Essential. See Object documentation
	whatIsAPrimitive."

	<primitive: 61>
	index isInteger
		ifTrue: [self errorSubscriptBounds: index]
		ifFalse: [self errorNonIntegerIndex]
]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>species [
	"Answer the preferred class for reconstructing the receiver."
	^WideString

]

{
	#category : #private,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>string: aString [
	1 to: aString size do: [:j | 
		self privateAt: j put: (aString at: j) asInteger].
	^ self
]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>wordAt: index [
	<primitive: 60>
	^ (self basicAt: index).

]

{
	#category : #accessing,
	#timestamp : ' 8/31/2017 07:16:45'
}
WideSymbol>>wordAt: index put: anInteger [
	self errorNoModification.
]
